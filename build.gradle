plugins {
    id 'java'
    id 'net.ltgt.errorprone' version "3.1.0"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'me.illusion'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:3.1.0")

    // shadowjar all modules
    implementation(project(":SkyblockCore-Common"))
    implementation(project(":SkyblockCore-Proxy"))
    implementation(project(":SkyblockCore-Server"))
    implementation(project(":SkyblockCore-Spigot")) {
        exclude group: 'me.illusion', module: 'cosmos-plugin'
    }
    implementation(project(":SkyblockCore-BungeeCord"))
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}